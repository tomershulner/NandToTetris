// Class that reprsents the main character of the game

class Vegeta {
	field int x_location;
	field int y_location;

	field int x_size, height;

    field int current_location;

	// Physics 
	field int gravity;
	field int jump_velocity;
    
    constructor Vegeta new(int x, int y, int tgravity, int tjump) {
		// Initialize character coordinates
        let x_location = x;
		let y_location = y;
		let height = 64;
		do update_location();

		// Initialize game hysics
		let gravity = tgravity;
		let jump_velocity = tjump;

        return this;
    }

	method void update_location() {
		let current_location = (y_location* 32) + (x_location/16);
		return;
	}

	method void draw() {
		do Graphics.drawVegeta(current_location);
		return;
	}

	method void erase() {
		do Graphics.eraseVegeta(current_location);
		return;
	}

	method void fly() {
		do erase();
		let y_location = y_location - jump_velocity;
		do update_location();
		do draw();
		return;
	}

	method void land() {
		do Output.moveCursor(1,1);
		do Output.printInt(y_location);
		if (y_location < 192) {
			do erase();
			let y_location = y_location + gravity;
			do update_location();
			do draw();
		}
		return;
	}

	method int getY() {
		return y_location;
	}

	method int getHeight() {
		return height;
	}

	method void dispose() {
    	do Memory.deAlloc(this);
    	return;
	}
}
